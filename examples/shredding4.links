typename Task         = String;
typename Employee     = (name:String, tasks:[Task], salary:Int);
typename Contact      = (name:String, "client":Bool);
typename Department   = (name:String, employees:[Employee], contacts:[Contact]);
typename Organisation = [Department];

var db = database "organisation";

var keytasks    = table "keytasks"    with (tsk : String)                from db;
var tasks       = table "tasks"       with (emp : String, tsk : String)  from db;
var employees   = table "employees"  
                  with (dpt : String, emp : String, salary : Int)        from db;
var contacts    = table "contacts"
                  with (dpt : String, contact : String, "client" : Bool) from db;
var departments = table "departments" with (dpt : String)                from db;

# construct a nested representation of the organisation
sig organisation : () -> Organisation
fun organisation() {
  for (x <-- departments)
    [(name=x.dpt,
      employees=
        for (y <-- employees) where (x.dpt == y.dpt)
          [(name=y.emp,
            tasks=
              for (z <-- tasks)
              where (y.emp == z.emp)
                [z.tsk],
            salary=y.salary)],
      contacts=
       for (y <-- contacts) where (x.dpt == y.dpt)
         [(name=y.contact, "client"=y."client")])]
}




sig emptyDepartments : (() {}-> (Organisation)) -> [(name:String)]
fun emptyDepartments(org) {
  query {
  for (x <- org()) 
  where (empty(x.employees))
    [(name = x.name)]
  }
}


emptyDepartments(organisation)

